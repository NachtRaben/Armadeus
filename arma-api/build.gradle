plugins {
    id 'idea'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow'
    id 'nu.studer.jooq' version '5.2.1'
}

sourceSets {
    ap {
        compileClasspath += main.compileClasspath + main.output
    }
}

jar {
    from sourceSets.ap.output
}

shadowJar.classifier = ''

dependencies {
    // Database Drivers
    jooqGenerator group: 'org.postgresql', name: 'postgresql', version: '42.2.19'
    api group: 'org.postgresql', name: 'postgresql', version: '42.2.19'
    api group: 'org.jooq', name: 'jooq', version: '3.14.8'
    api group: 'com.velocitypowered', name: 'velocity-utils-api', version: '4.0.0-SNAPSHOT', changing: true

    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    // Logging API Dependencies
    api group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion

    // Discord API Dependencies
    api(group: 'net.dv8tion', name: 'JDA', version: jdaVersion, changing: true) {
        exclude module: 'opus-java'
    }

    // Command API Dependencies
    api group: 'co.aikar', name: 'acf-core', version: '0.5.0-SNAPSHOT'

    // Config API Dependencies
    api group: 'com.electronwill.night-config', name: 'toml', version: '3.6.3'
    api group: 'com.electronwill.night-config', name: 'json', version: '3.6.3'

    // Utilities
    api group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    api group: 'com.google.guava', name: 'guava', version: '30.0-jre'
    api group: 'com.google.inject', name: 'guice', version: '5.0.1'
//    api group: 'org.reflections', name: 'reflections', version: '0.9.12'
}

jooq {
    version = '3.14.8'
    edition = nu.studer.gradle.jooq.JooqEdition.OSS
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.INFO
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = postgresUrl
                    user = postgresUser
                    password = postgresPass
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        includes = 'guilds'
                        excludes = ''
                        inputSchema = 'public'
                    }
                    target {
                        packageName = 'dev.armadeus.bot.database'
                        directory = 'src/database/java'  // default (can be omitted)
                    }
                }
            }
        }
    }
}

publishing {
    repositories {
        repositories {
            maven {
                credentials {
                    username "$nexusUser"
                    password "$nexusPass"
                }
                if (!project.version.endsWith('-SNAPSHOT')) {
                    url "https://nachtraben.com/nexus/repository/maven-releases"
                } else {
                    url "https://nachtraben.com/nexus/repository/maven-snapshots"
                }
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = rootProject.group
            artifactId = 'arma-api'
            version = version
            artifact shadowJar
            artifact sourcesJar
        }
    }
}


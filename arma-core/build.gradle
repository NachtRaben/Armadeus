import com.github.jengelman.gradle.plugins.shadow.transformers.Log4j2PluginsCacheFileTransformer

plugins {
    id 'idea'
    id 'java-library'
    id 'com.github.johnrengelman.shadow'
    id 'nu.studer.jooq' version '5.2.1'
}

jar {
    manifest {
        attributes "Main-Class": "dev.armadeus.core.BootLoader"
        attributes 'Implementation-Title': "ArmaCore"
        attributes 'Implementation-Version': version
        attributes 'Implementation-Vendor': "Armadeus Devs"
        attributes 'Multi-Release': 'true'
        attributes 'Add-Opens': 'java.base/java.lang'
    }
}

shadowJar {
    transform(Log4j2PluginsCacheFileTransformer)
}

dependencies {
    // Jooq
    jooqGenerator group: 'org.postgresql', name: 'postgresql', version: '42.2.19'
    // Lombok
    compileOnly group: 'org.projectlombok', name: 'lombok', version: lombokVersion
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: lombokVersion

    // API
    implementation project(':arma-api')
    implementation group: 'com.velocitypowered', name: 'velocity-utils', version: '4.0.0-SNAPSHOT', changing: true

    // VelocityImpl
//    implementation 'org.lanternpowered:lmbda:2.0.0-SNAPSHOT'
//    implementation 'com.github.ben-manes.caffeine:caffeine:2.8.8'

    // DatabaseImpl
    implementation group: 'com.zaxxer', name: 'HikariCP', version: '4.0.3'

    // LoggingImpl
    implementation "org.apache.logging.log4j:log4j-api:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-core:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-iostreams:${log4jVersion}"
    implementation "org.apache.logging.log4j:log4j-jul:${log4jVersion}"
    implementation 'com.lmax:disruptor:3.4.2' // Async loggers

    // Utility
    api group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '5.0.4' // Command Line Args
    implementation group: 'org.spongepowered', name: 'configurate-hocon', version: configurateVersion // Configuration Files
    implementation group: 'org.spongepowered', name: 'configurate-gson', version: configurateVersion // Configuration Files

    api group: 'com.google.guava', name: 'guava', version: '30.0-jre' // TODO: Phase out
}

shadowJar {
    mergeServiceFiles()
}

jooq {
    version = '3.14.8'
    edition = nu.studer.gradle.jooq.JooqEdition.OSS
    configurations {
        main {
            generateSchemaSourceOnCompilation = false
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.INFO
                jdbc {
                    driver = 'org.postgresql.Driver'
                    url = postgresUrl
                    user = postgresUser
                    password = postgresPass
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.postgres.PostgresDatabase'
                        includes = 'instances'
                        excludes = ''
                        inputSchema = 'public'
                    }
                    target {
                        packageName = 'dev.armadeus.bot.database.core'
                        directory = 'src/database/java'  // default (can be omitted)
                    }
                }
            }
        }
    }
}
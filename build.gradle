buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "gradle.plugin.com.github.jengelman.gradle.plugins:shadow:7.0.0"
    }

    apply from: "https://nachtraben.com/files/commons.gradle"

    ext {
        nexusUser = System.getenv("NEXUS_USR") ?: loadProperty("nexus.nachtraben.user")
        nexusPass = System.getenv("NEXUS_PSW") ?: loadProperty("nexus.nachtraben.pass")
        postgresUser = loadProperty("postgres.dev.armadeus.user")
        postgresPass = loadProperty("postgres.dev.armadeus.pass")
        postgresUrl = loadProperty("postgres.dev.armadeus.url")
    }
}

allprojects {
    apply plugin: 'idea'
    apply plugin: 'java-library'

    group 'dev.armadeus'
    version GIT_BRANCH

    ext {
        lombokVersion = '1.18.20'
        slf4jVersion = '1.7.30'
        log4jVersion = '2.14.1'
        guavaVersion = '30.1.1-jre'
        jdaVersion = '4.3.0_324'
        jdaDevVersion = 'feature~slash-commands-SNAPSHOT'
        lavaplayerVersion = '1.3.76'
        groovyVersion = '3.0.7'
    }

    java {
        sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }

    compileJava {
        options.compilerArgs += ["-parameters"]
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    compileJava.options.encoding = javadoc.options.encoding = "UTF-8"

    repositories {
        mavenCentral()
        maven {
            name = "NachtRaben-Nexus"
            url = "https://nachtraben.com/nexus/repository/maven-public/"
        }
        // JDA Dependencies
        maven { name 'm2-dv8tion'; url 'https://m2.dv8tion.net/releases' }
        // Aikar Dependencies
        maven { name "Aikar"; url "https://repo.aikar.co/content/groups/aikar/" }
        // Lmbda
        maven { name "Sonatype"; url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        archiveName = project.name + '-sources.jar'
        from project.sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        archiveName = rootProject.name + '-javadoc.jar'
        from javadoc.destinationDir
    }
}

